@detectDelim
@top CriticMarkup { (expression|char)* }

expression {
  Addition |
  Deletion |
  Substitution |
  Comment |
  Highlight
}

@skip {} {
  Addition { lAdd (additionContent? MDSepAdd)? additionContent? rAdd }
  Deletion { lDel (deletionContent? MDSepDel)? deletionContent? rDel }
  Substitution { lSub (substitutionContent? MDSepSub)? substitutionContent? MSub substitutionContent? rSub }
  Comment { lCom  (commentContent? MDSepCom)? commentContent? rCom }
  Highlight { lHig (highlightContent? MDSepHig)? highlightContent? rHig }
}

@tokens {
  lAdd { "{++" }
  lDel { "{--" }
  lSub { "{~~" | "{<del>" }
  lCom { "{>>" | "{&gt;&gt;" }
  lHig { "{==" | "{<mark>" }

  char { $[\n\r\t\u{20}\u{21}\u{23}-\u{5b}\u{5d}-\u{10ffff}\\"] | "\\" esc }
  esc  { $["\\\/bfnrt] | "u" hex hex hex hex }
  hex  { $[0-9a-fA-F] }
}

@local tokens {
  MDSepAdd { "@@" }
  rAdd { "++}" }
  @else additionContent
}

@local tokens {
  MDSepDel { "@@" }
  rDel { "--}" }
  @else deletionContent
}

@local tokens {
  MDSepSub { "@@" }
  MSub { "~>" | "~&gt;"}
  rSub { "~~}" | "</del>}" }
  @else substitutionContent
}

@local tokens {
  MDSepCom { "@@" }
  rCom { "<<}" | "&lt;&lt;}" }
  @else commentContent
}

@local tokens {
  MDSepHig { "@@" }
  rHig { "==}" | "</mark>}" }
  @else highlightContent
}
