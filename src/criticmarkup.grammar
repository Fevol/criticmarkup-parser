@detectDelim
@top CriticMarkup { (char|expression)* }

expression {
  Addition |
  Deletion |
  Substitution |
  Comment |
  Highlight
}

@skip {} {
  Addition { lAdd additionContent? rAdd }
  Deletion { lDel deletionContent? rDel }
  Substitution { lSub substitutionContent? MSub substitutionContent? rSub }
  Comment { lCom commentContent? rCom }
  Highlight { lHig highlightContent? rHig }
}

@tokens {
  lAdd { "{++" }
  lDel { "{--" }
  lSub { "{~~" }
  lCom { "{>>" }
  lHig { "{==" }

  char { $[\n\r\t\u{20}\u{21}\u{23}-\u{5b}\u{5d}-\u{10ffff}\\] | "\\" esc }
  esc  { $["\\\/bfnrt] | "u" hex hex hex hex }
  hex  { $[0-9a-fA-F] }
}

@local tokens {
  rAdd { "++}" }
  @else additionContent
}

@local tokens {
  rDel { "--}" }
  @else deletionContent
}

@local tokens {
  MSub { "~>" }
  rSub { "~~}" }
  @else substitutionContent
}

@local tokens {
  rCom { "<<}" }
  @else commentContent
}

@local tokens {
  rHig { "==}" }
  @else highlightContent
}